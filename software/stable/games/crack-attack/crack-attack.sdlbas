#!/usr/local/bin/sdlBasic
'
' sdlBasic game: Crack-Attack-solo
'
'  Name:      crack-attack.sdlbas
'  Purpose:   clone of the original opengl crack-attack
'  Author:     __vroby__    ( __vroby__@libero.it )
'  Licence:    LGPL
'
' Bugs:
'
' Todo:
'
'
'-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

setdisplay(640,480,32,0)



autoback (0)
mousehide
common map[12,6]
common px,py
common gx
common t
common level,ls,score

common hiscore[100,2]
'___________________________________________________________________________

sub savescore
    open "crack-attack.score" for output as 1
    for i =1 to 100
	print #1,hiscore[i,0]
	print #1,hiscore[i,1]
	print #1,hiscore[i,2]
    next
    close 1

end sub
'___________________________________________________________________________

sub loadscore
    if fileexists("crack-attack.score") then
	open "crack-attack.score" for input as 1
	for i =1 to 100
	    file input #1,hiscore[i,0]
	    file input #1,hiscore[i,1]
	    file input #1,hiscore[i,2]
	next
	close 1
    end if
end sub
'___________________________________________________________________________

sub init
    loadimage("graphics/logo.png",1)
    sprite(1,10,10,1)
    loadimage("graphics/count_down_3.png",11)
    loadimage("graphics/count_down_2.png",12)
    loadimage("graphics/count_down_1.png",13)

    loadimage("graphics/sign_x2.png",102)
    loadimage("graphics/sign_x3.png",103)
    loadimage("graphics/sign_x4.png",104)
    loadimage("graphics/sign_x5.png",105)
    loadimage("graphics/sign_x6.png",106)
    loadimage("graphics/sign_x7.png",107)
    loadimage("graphics/sign_x8.png",108)
    loadimage("graphics/sign_x9.png",109)
    loadimage("graphics/sign_x10.png",110)
    loadimage("graphics/sign_x11.png",111)
    loadimage("graphics/sign_x11.png",112)





    loadsound("sounds/countdown.wav",1)
    loadsound("sounds/block_awaking.wav",2)
    loadsound("sounds/block_dying.wav",3)
    loadsound("sounds/block_fallen.wav",4)



    loadscore
end sub
'___________________________________________________________________________

sub startgame
    randomize(timer)
    for y= 0 to 12
	for x =0 to 5
	    m=rnd(100)
	    if (m>90) then: m=5:end if
	    if (m>80) then: m=4:end if
	    if (m>70) then: m=3:end if
	    if (m>60) then: m=2:end if
	    if (m>50) then: m=1:end if
	    if (m>5) then: m=0:end if
	    map[y,x]=m
	next
    next
    px=1 :py=1
    score =0

    for i= 1 to 3
	sprite(2,320,200,10+i)
	playsound(1,1)
	screenswap
	wait (1000)
    next
    deletesprite(2)
    level=0
    ls=0


end sub
'___________________________________________________________________________

sub drawtile(x,y,m,d)


    if d<4 then :d=4:end if

    ink (color(m))
    h=12*32
    bar(x*32+2,h-(y*32)+2,(x*32)+d-2,h-(y*32)+d-2)
    'screenswap
    'waitkey

    if m>0 then
	ink (0xf0f0f0)
	bar(x*32,h-(y*32),x*32+d,h-(y*32)+2)
	bar(x*32,h-(y*32),x*32+2,h-(y*32)+d)

	ink (0x808080)
	bar( (x*32)+d-2,h-(y*32)+2,(x*32)+d,h-(y*32)+d)
	bar(x*32+2,h-(y*32)+d-2,(x*32)+d,h-(y*32)+d)
   end if
end sub
'___________________________________________________________________________

sub downmap
    for y= 12 to 1 step -1
	for x =0 to 5
	    if 	map[y-1,x]=0 then
		if map[y,x]<>0 then : playsound(1,1): end if
		for i = y to 12
		    map[i-1,x]=map[i,x]
		next
		map[12,x]=0
	    end if
	next
    next
end sub
'___________________________________________________________________________

sub drawmap
    h=32*12
    cls
    for y= 0 to 12
	for x =0 to 5
	    d=31
	    if map[y,x]<0 then
		map[y,x]=map[y,x]+1
		d=abs(map[y,x])

	    end if

	    drawtile (x,y,abs(map[y,x]),d)

	next
    next
    for i = 2 to 32 step 2
	ink(0xffffff):line(0,h+i,6*32,h+i)
    next
    for i = 0 to 6*32 step 2
	ink(0):line(i,h+2,i,h+30)
    next

end sub
'___________________________________________________________________________

sub clearblock

'verify gameover
	for x =0 to 5
	    if map[12,x]<>0 then :gx=1:end if
	next
'vertical verify
    for y= 12 to 3 step -1
	for x =0 to 5
	    c=map[y,x]
	    if c<>0 and map[y-1,x]=c and map[y-2,x]=c then
		si=y
		for i = y to 1 step -1
		    if map[i,x]=c then
			map[i,x]=-31
		    else
			si=i
			i=0
		    end if
		next
		ls=ls+1
		score=score+((y-si)*ls)
		if ls>1 then :sprite(5,420,240,100+ls):end if
		playsound(2,1)
		for i = 32 to 0 step -1
		    drawmap
		    screenswap
		    wait(5)
		next
	    end if
	next
    next

'horizontal verify
    for y= 12 to 1 step -1
	for x =0 to 3
	    c=map[y,x]
	    if c<>0 and map[y,x+1]=c and map[y,x+2]=c then
		si=5
		for i = x to 5
		    if map[y,i]=c then
			map[y,i]=-31
		    else
			si=i
			i=5
		    end if
		next
		ls=ls+1
		score=score+((si)*ls)
		if ls>1 then :sprite(5,420,240,100+ls):end if
		playsound(2,1)
		for i= 32 to 0 step -1
		drawmap
		screenswap
		    wait(5)
		next
	    end if
	next
    next

if ls=0 then : deletesprite(5) : end if

end sub
'___________________________________________________________________________

sub newline
    for y= 12 to 1 step -1
	for x =0 to 5
	    map[y,x]=map[y-1,x]
	next
    next
	for x =0 to 5
	    m=rnd(55)
	    if (m>45) then: m=5:end if
	    if (m>35) then: m=4:end if
	    if (m>25) then: m=3:end if
	    if (m>15) then: m=2:end if
	    if (m>5) then: m=1:end if
	    map[0,x]=m
	next
end sub
'___________________________________________________________________________

sub player
    h=32*12
    ink (rgb(255,255,255))
    box (px*32,h-(py*32),px*32+64,h-(py*32)+31)
    if (t/4 mod 2)=1 then
	ink (rgb(255,0,0))
    else
	ink (rgb(255,255,0))
    end if


    box (px*32+1,h-(py*32)+1,px*32+64-1,h-(py*32)+31-1)
    box (px*32+2,h-(py*32)+2,px*32+64-2,h-(py*32)+31-2)

	if key(k_down) then: py=py-1:ls=0 :end if
	if key(k_up) then: py=py+1:ls=0 :end if
	if py<1 then: py=1 :ls=0 :end if
	if py>12 then: py=12 :ls=0 :end if

	if key(k_left) then: px=px-1:end if
	if key(k_right) then: px=px+1:end if
	if px<0 then: px=0 :end if
	if px>4 then: px=4 :end if

	if key(asc(" ")) then
	    c=map[py,px]
	    map[py,px]=map[py,px+1]
	    map[py,px+1]=c
	    ls=0
	end if
    tw=timer+150
    while key(k_up)<>0 and tw>timer :end while
    while key(k_down)<>0 and tw>timer:end while
    while key(k_left)<>0 and tw>timer:end while
    while key(k_right)<>0 and tw>timer:end while
    while key(asc(" "))<>0 and tw>timer:end while

end sub
'___________________________________________________________________________

sub game
    'loadmusic("music/game.ogg")
    'playmusic(-1)

    screenopen(1,320,480,320,40,6*32,12*32,0)
    startgame
    t=0:gx=0
    while gx=0
	v=500-(10*level):if v<1 then :v=1:end if
	offset(0,(32*t/v))
	downmap
	drawmap
	clearblock
	player
	'wait(50)
	t=t+1
	screen(0)
	locate (5,31)
	prints("next level at :"+str$(500-level-t)+"   ")
	if t>(500-(10*level))then
	    t=0
	    level=level+1
	    newline
	    py=py+1
	end if

	ink (0)
	bar (0,200,300,400)
	ink (rgb(255,0,0))
	text(50,280,20,"level:"+str$(level))
	ink (rgb(0,255,0))
	text(50,320,20,"score:"+str$(score))


	screen(1)

	screenswap
	if key(27) then:gx=1:end if
    end while
    screenclose(1)
end sub
'___________________________________________________________________________

sub title
'loadmusic("music/prelude.ogg")
'playmusic(-1)
screen(0)
cls
text(320,250,26, "press any key to start" )
screenswap
waitkey
cls
end sub
'___________________________________________________________________________

sub insertscore
    if hiscore[100,0]<score then
	hiscore[100,0]=score
	hiscore[100,1]=level
	hiscore[100,2]= date
	for i = 100 to 2 step -1
	    for i1 = i to 1 step -1
		if val(hiscore[i,0])>val(hiscore[i1,0]) then
		    for l =0 to 2
			tmp=hiscore[i,l]
			hiscore[i,l]=hiscore[i1,l]
			hiscore[i1,l]=tmp
		    next
		end if
	    next
	next
    end if

end sub
'___________________________________________________________________________

sub showscore
    for i=1 to 100
	if hiscore[i,0]=score then
	    s=i
	    i=100
	end if
    next
    i=s-5
    if i<1 then :i=1:end if
    if i+10 >100 then :i=90:end if

    l=0
    for i1=i to i+10
	ink(0xffffff)
	if i1=s then:ink(0xffff00):end if

	text(320,100+l,16,str$(i1))
	text(350,100+l,16,str$(hiscore[i1,0]))
	text(420,100+l,16,str$(hiscore[i1,1]))
	text(500,100+l,16,hiscore[i1,2])
	l=l+20
    next

end sub
'___________________________________________________________________________

sub gameover
    'loadmusic("music/prelude.ogg")
    'playmusic(-1)
    'screen(0)
    insertscore
    showscore
    savescore

    text(50,250,26, "        game over     " )
    screenswap
    waitkey
    cls
end sub
'___________________________________________________________________________

sub main
    init
    while key(27)=0
	title
	game
	gameover
    end while
end sub
'___________________________________________________________________________

main
